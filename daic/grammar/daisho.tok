
///////////////
// Operators //
///////////////

// Note: << and >> are handled in the parser.
// This is helpful for parsing templates.
PLUS:   "+";
MINUS:  "-";
STAR:    "*";
DIV:    "/";
MOD:    "%";
AND:    "&";
OR:     "|";
XOR:    "^";
NOT:    "!";
BITNOT: "~";
LOGAND: "&&";
LOGOR:  "||";

DEQ:    "==";
NEQ:    "!=";
LT:     "<";
LEQ:    "<=";
GT:     ">";
GEQ:    ">=";

EQ:     "=";
PLEQ:   "+=";
MINEQ:  "-=";
MULEQ:  "*=";
DIVEQ:  "/=";
MODEQ:  "%=";
ANDEQ:  "&=";
OREQ:   "|=";
XOREQ:  "^=";
BNEQ:   "~=";
BSREQ:  ">>=";
BSLEQ:  "<<=";

INCR:   "++";
DECR:   "--";
QUEST:  "?";
COLON:  ":";
NCOLL:  "?:";


//////////////////
// Control Flow //
//////////////////
//IF: "if";
//ELSE: "else";
//DO: "do";
FOR: "for";
IN: "in";
WHILE: "while";
THEN: "then";
ALSO: "also";


/////////
// OOP //
/////////

UNION: "union";
TRAIT: "trait";
DYN:   "dyn";
CTYPE: "ctype";
CFUNC: "cfunc";
SELFTYPE: "Self";
SELFVAR: "self";
VOIDTYPE: "Void";

/////////////////
// Misc Syntax //
/////////////////

SEMI:    ";";
DOT:     ".";
COMMA:   ",";
OPEN:    "(";
CLOSE:   ")";
LCBRACK: "{";
RCBRACK: "}";
LSBRACK: "[";
RSBRACK: "]";
HASH:    "#";
REF:     "@";
DEREF:   "$";
GRAVE:   "`";


///////////////////////
// Abbreviated Words //
///////////////////////

// ret(urn)?
RET: (3, 6) {
  (0, 'r') -> 1;
  (1, 'e') -> 2;
  (2, 't') -> 3;
  (3, 'u') -> 4;
  (4, 'r') -> 5;
  (5, 'n') -> 6;
};

// impl([ei]ments)?
IMPL:  (4, 10) {
  (0, 'i') -> 1;
  (1, 'm') -> 2;
  (2, 'p') -> 3;
  (3, 'l') -> 4;
  (4, 'e') -> 5;
  (4, 'i') -> 5;
  (5, 'm') -> 6;
  (6, 'e') -> 7;
  (7, 'n') -> 8;
  (8, 't') -> 9;
  (9, 's') -> 10;
};

// op(erator)?
OP: (2, 8) {
  (0, 'o') -> 1;
  (1, 'p') -> 2;
  (2, 'e') -> 3;
  (3, 'r') -> 4;
  (4, 'a') -> 5;
  (5, 't') -> 6;
  (6, 'o') -> 7;
  (7, 'r') -> 8;
};

// redef(ine)?
REDEF: (5, 8) {
  (0, 'r') -> 1;
  (1, 'e') -> 2;
  (2, 'd') -> 3;
  (3, 'e') -> 4;
  (4, 'f') -> 5;
  (5, 'i') -> 6;
  (6, 'n') -> 7;
  (7, 'e') -> 8;
};

// class|struct|type
STRUCT: (5, 11, 16) {
  (0, 'c') -> 1;
  (1, 'l') -> 2;
  (2, 'a') -> 3;
  (3, 's') -> 4;
  (4, 's') -> 5;
  (0, 's') -> 6;
  (6, 't') -> 7;
  (7, 'r') -> 8;
  (8, 'u') -> 9;
  (9, 'c') -> 10;
  (10, 't') -> 11;
  (0,  't') -> 13;
  (13, 'y') -> 14;
  (14, 'p') -> 15;
  (15, 'e') -> 16;
};

/////////////////////////
// Numbers and Strings //
/////////////////////////

// Starts with capital
TYPEIDENT: (1, 2) {
  (0, [A-Z]) -> 1;
  ((1, 2), [_a-zA-Zα-ωΑ-Ω0-9]) -> 2;
};

// Starts with lowercase (or noncapital)
VARIDENT: (1, 2) {
  (0, [_a-zα-ωΑ-Ω]) -> 1;
  ((1, 2), [_a-zA-Zα-ωΑ-Ω0-9]) -> 2;
};

CIDENT: (1, 2) {
  (0, [_a-z]) -> 1;
  ((1, 2), [_a-zA-Z0-9]) -> 2;
};

// [-+]?[0-9]+(\.[0-9]+)?
NUMLIT: (2, 3) {
  (0, [-+]) -> 1;
  ((0, 1, 2), [0-9]) -> 2;
  (2, '.') -> 3;
  (3, [0-9]) -> 3;
};

// ["]([^"\n\\]|\\[nfbrte\\'"])*["]
STRLIT: 2 {
  (0, ["])  -> 1;
  (1, ["])  -> 2;
  (1, [\n]) -> 9; // no newlines in string.
  (1, [\\]) -> 3; // start parsing escape sequence
  (1, [^])  -> 1; // string content
  (3, [n])  -> 1; // newline
  (3, [f])  -> 1; // form feed
  (3, [b])  -> 1; // backspace
  (3, [r])  -> 1; // carriage return
  (3, [t])  -> 1; // tab
  (3, [e])  -> 1; // escape character
  (3, [\\]) -> 1; // backslash
  (3, ['])  -> 1; // single quote
  (3, ["])  -> 1; // double quote
};


///////////////////////////////////////
// Whitespace, Comments, and Shebang //
///////////////////////////////////////

// [\ \n\r\t]+
WS: 1 {
  ((0, 1), [ \n\r\t]) -> 1;
};

// \/\*([^*]*\*)*\/
MLCOM: 4 {
  (0, '/') -> 1;
  (1, '*') -> 2;
  (2, '*') -> 3;
  (2, [^]) -> 2;
  (3, '*') -> 3;
  (3, '/') -> 4;
  (3, [^]) -> 2;
};

// \/\/[^\n]*
SLCOM: (2, 3) {
  (0, '/') -> 1;
  (1, '/') -> 2;
  (2, [^\n]) -> 2;
  (2, [\n]) -> 3;
};

// #![^\n]*[\n]
SHEBANG: 3 {
  (0, '#') -> 1;
  (1, '!') -> 2;
  (2, [^\n]) -> 2;
  (2, [\n]) -> 3;
};
