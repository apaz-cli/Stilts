%include <assert.h>
%extra     void* extra;
%extra     void* symtab;
%extra     void* type;
%extrainit node->extra = NULL;
%extrainit node->symtab = NULL;
%extrainit node->type = NULL;
%postinclude "../asthelpers.h"

file      <- sh:SHEBANG? topdecl (SEMI topdecl)*

/****************/
/* DECLARATIONS */
/****************/

topdecl    <- structdecl / uniondecl / traitdecl / fndecl / impldecl / nsdecl


structdecl <- STRUCT id:STRUCTIDENT tmpl:tmpldecl?
              impl:(IMPL type (COMMA type)*)?
              LCBRACK
              members:{ret=list(MEMBERLIST)}
             (m:typemember {add(members, m)})*
              RCBRACK
              n:{n = node(STRUCT, id, members);
              rule = has(tmpl) ? node(TMPLSTRUCT, tmpl, n) : n}

uniondecl  <- UNION id:STRUCTIDENT tmpl:tmplexpand?
              impl:(IMPL type (COMMA type)*)?
              LCBRACK
              members:{ret=list(MEMBERLIST)}
             (m:typemember {add(members, m)})*
              RCBRACK
              n:{n = node(UNION, id, members);
              rule = has(tmpl) ? node(TMPLUNION, tmpl, n) : n}

traitdecl  <- TRAIT id:STRUCTIDENT tmpl:tmplexpand?
              impl:(IMPL type (COMMA type)*)?
              LCBRACK
              members:{ret=list(MEMBERLIST)}
             (m:fnmember {add(members, m)})*
              RCBRACK
              n:{n = node(TRAIT, id, members);
              rule = has(tmpl) ? node(TMPLTRAIT, tmpl, n) : n}

fndecl     <- FN fnproto tmplexpand? expr

impldecl   <- IMPL tt:STRUCTIDENT tmplexpand?
              FOR ft:type
              LCBRACK
              members:{ret=list(MEMBERLIST)}
             (m:fnmember {add(members, m)})*
              RCBRACK

nsdecl     <- ns:NAMESPACE t:STRUCTIDENT {rule=node(NAMESPACE, t)}

typemember <- t:type v:VARIDENT SEMI {rule=node(TYPEMEMBER, t, v)}

fnmember   <- r:(fndecl / fnproto) SEMI {rule=r}


/*********/
/* TYPES */
/*********/

// a type, not a type definition
type<uint8_t depth = 0>
           <- v:VOIDTYPE     !STAR              {rule=node(TYPE, v)} // Void means nothing.
            / v:voidptr      (STAR {depth++})*  {rule=node(TYPE, v)} // Void* means anything.
            / s:SELFTYPE     (STAR {depth++})*  {rule=node(TYPE, s)} // Self refers to the type of the first arg, or the current type.
            / t:traittype    (STAR {depth++})*  {rule=node(TYPE, t)} // Trait object (+1 pointer, trait)
            / s:structtype   (STAR {depth++})*  {rule=node(TYPE, s)} // Type identifier
            / f:fntype       (STAR {depth++})*  {rule=node(TYPE, f)} // Fn<From -> To> is To(*fptr)(From).
            / c:ctypedecl                       {rule=node(TYPE, c)} // TODO figure out syntax

voidptr    <- v:VOIDPTR         {rule=v}
            / v:VOIDTYPE s:STAR {rule=leaf(VOIDPTR)}

traittype  <- i:STRUCTIDENT t:tmplexpand?
              {rule = has(t) ? node(TMPLTYPE, t, i) : node(TYPE, i)}

structtype <- s:STRUCTIDENT t:tmplexpand?
              {rule = has(t) ? node(TMPLTYPE, t, s) : node(TYPE, s)}

ctypedecl  <- CTYPE CIDENT

fntype     <- FNTYPE
              LT
              argtypes:{ret=list(ARGLIST)}
              (      t:type {add(argtypes, t)})?
              (COMMA t:type {add(argtypes, t)})*
              {if (!argtypes->num_children) add(argtypes, node(TYPE, leaf(VOIDTYPE)))}
              (arrow:ARROW rettype:type)?
              GT
              {rule=node(FNTYPE,
                         argtypes,
                         !has(rettype) ? node(TYPE, leaf(VOIDTYPE)) : rettype)}
              /
              FNTYPE
              tmp:{rule=node(FNTYPE,
                            (tmp=list(ARGLIST), add(tmp, node(TYPE, leaf(VOIDTYPE))), tmp),
                             node(TYPE, leaf(VOIDTYPE)))}


/***********************/
/* FUNCTIONS AND TYPES */
/***********************/

tmpldecl   <- TEMPLATE

tmplspec   <-

tmplexpand <- LT tmplmember? (COMMA tmplmember)* GT

tmplmember <- type

fnproto    <- type OPEN fnarg? (COMMA fnarg)* CLOSE

fnarg      <- type (VARIDENT tmplexpand?)?

fnbody     <- expr


/****************************/
/* CONTROL FLOW EXPRESSIONS */
/****************************/

expr       <- next

%node IF
%node IFELSE
ifeexpr    <- IF cond:ternexpr ex:expr (ELSE eex:expr)?
              {rule= !has(eex) ? srepr(node(IF, cond, ex), "if") : srepr(node(IFELSE, cond, ex, eex), "if-else")}
            / n:next {rule=n}

forexpr    <- FOR OPEN? n:next ((COLON / IN) / (SEMI sn:next SEMI))  n:next CLOSE? n:next
            / n:next {rule=n}

whileexpr  <- WHILE o:OPEN? n:next c:CLOSE? {ret=o==c?SUCC:NULL} expr
            / n:next {rule=n}

ternexpr   <- n:next (q:QUEST qe:expr (c:COLON ce:expr)?)?
              {rule = !has(qe) ? n
                    : !has(ce) ? node(ELVIS, q, n, qe)
                    :            node(TERN, q, c, n, qe, ce)}

thenexpr   <- n:next {rule=n} (o:THEN nn:next {rule=srepr(node(THEN, rule, nn), "then")})*

alsoexpr   <- n:next {rule=n} (o:ALSO nn:next {rule=srepr(node(ALSO, rule, nn), "also")})*


/********************/
/* BINARY OPERATORS */
/********************/

binop      <- next

eqexpr     <- n:next {rule=n}
             (op:(EQ / PLEQ / MINEQ / MULEQ / DIVEQ / MODEQ / ANDEQ / OREQ / XOREQ / BNEQ / BSREQ / BSLEQ)
              t:next
              // Requires information about what the next expression is
              {
                if      (op->kind == kind(EQ))    rule=node(EQ, rule,                       t );
                else if (op->kind == kind(PLEQ))  rule=node(EQ, rule, node(PLUS,  rule, t));
                else if (op->kind == kind(MINEQ)) rule=node(EQ, rule, node(MINUS, rule, t));
                else if (op->kind == kind(MULEQ)) rule=node(EQ, rule, node(MUL,   rule, t));
                else if (op->kind == kind(DIVEQ)) rule=node(EQ, rule, node(DIV,   rule, t));
                else if (op->kind == kind(MODEQ)) rule=node(EQ, rule, node(MOD,   rule, t));
                else if (op->kind == kind(ANDEQ)) rule=node(EQ, rule, node(AND,   rule, t));
                else if (op->kind == kind(OREQ))  rule=node(EQ, rule, node(OR,    rule, t));
                else if (op->kind == kind(XOREQ)) rule=node(EQ, rule, node(BNEQ,  rule, t));
                else if (op->kind == kind(BSREQ)) rule=node(EQ, rule, node(BSR,   rule, t));
                else if (op->kind == kind(BSLEQ)) rule=node(EQ, rule, node(BSL,   rule, t));
                else
                  #if defined(__DAI_UNREACHABLE)
                    __DAI_UNREACHABLE()
                  #else
                    assert(!"Unexpected node type.")
                  #endif
              })*

logorexpr  <- n:next {rule=n} (LOGOR  n:next {rule=srepr(node(LOGOR,  rule, n), "||")})*
logandexpr <- n:next {rule=n} (LOGAND n:next {rule=srepr(node(LOGAND, rule, n), "&&")})*
binorexpr  <- n:next {rule=n} (OR     n:next {rule=srepr(node(OR,     rule, n), "|")})*
binxorexpr <- n:next {rule=n} (XOR    n:next {rule=srepr(node(XOR,    rule, n), "^")})*
binandexpr <- n:next {rule=n} (AND    n:next {rule=srepr(node(AND,    rule, n), "&")})*

deneqexpr  <- n:next {rule=n}
             (DEQ n:next {rule=srepr(node(DEQ, rule, n), "==")}
             /NEQ n:next {rule=srepr(node(NEQ, rule, n), "!=")})*

cmpexpr    <- n:next {rule=n}
             (LT  n:next {rule=srepr(node(LT,  rule, n), "<")}
             /GT  n:next {rule=srepr(node(GT,  rule, n), ">")}
             /LEQ n:next {rule=srepr(node(LEQ, rule, n), "<=")}
             /GEQ n:next {rule=srepr(node(GEQ, rule, n), ">=")})*

sumexpr    <- n:next {rule=n}
             (PLUS  n:next {rule=srepr(node(PLUS,  rule, n), "+")}
             /MINUS n:next {rule=srepr(node(MINUS, rule, n), "-")})*

multexpr   <- n:next {rule=n}
             (STAR n:next {rule=srepr(node(STAR, rule, n), "*")}
             /DIV  n:next {rule=srepr(node(DIV,  rule, n), "/")}
             /MOD  n:next {rule=srepr(node(MOD,  rule, n), "%")})*

powexpr    <- n:next {rule=n} (POW {rule=srepr(node(POW, rule, n), "**")})*


shfexpr    <- n:next {rule=n} // Tokenized as << and >>.
             (LT LT n:next {rule=srepr(node(BSL, rule, n), "<<")}
             /GT GT n:next {rule=srepr(node(BSR, rule, n), ">>")})*

callexpr   <- vi:VARIDENT t:tmplexpand?
              (args:fncallargs
              {rule=node(CALL, vi, args); if (has(t)) {rule=node(TMPLCALL, rule, t);t=NULL;}})*
              {ret=!has(rule)?NULL:SUCC}
              /
              n:castexpr {rule=n}
              (args:fncallargs {rule=node(CALL, rule, args)})*

fncallargs <- OPEN l:{ret=rule=list(FNARGLIST)}
              e:expr? {if (has(e)) add(l, e)} (COMMA e:expr {add(l, e)})* COMMA?
              CLOSE

castexpr   <- n:next {rule=n} (OPEN t:type CLOSE {rule = srepr(node(CAST, rule, t), "cast")})* 

refexpr    <- n:next r:REF?    {rule=has(r) ? srepr(node(REF, n), "@") : n}

derefexpr  <- n:next d:DEREF?  {rule=has(d) ? srepr(node(REF, n), "$") : n}

postretexpr <- n:next g:GRAVE? {rule=has(g) ? srepr(node(RET, n), "return") : n}


/*********************************/
/* ATOMS AND SPECIAL EXPRESSIONS */
/*********************************/

atomexpr   <- blockexpr
            / lambdaexpr
            / listcomp
            / listlit
            / parenexpr
            / cfuncexpr
            / preretexpr
            / VARIDENT
            / numlit
            / strlit

blockexpr  <- LCBRACK
              {rule=list(BLK)}
              (e:expr {add(rule, e)})*
              RCBRACK

lambdaexpr <- (LSBRACK expr? (COMMA expr)* RSBRACK)? ((OPEN VARIDENT? (COMMA VARIDENT)* CLOSE) / (VARIDENT)) ARROW expr

listcomp   <- LSBRACK
              (cnt:VARIDENT COMMA)? expr   // Optional count
              FOR item:VARIDENT
              IN expr
              (WHERE expr)?
              RSBRACK

listlit    <- LSBRACK expr? (COMMA expr)* COMMA? RSBRACK

parenexpr  <- OPEN e:expr CLOSE {rule=e}

cfuncexpr  <- CFUNC CIDENT

preretexpr <- r:RET e:expr {rule=node(RET, r, e)}

numlit     <- pm:(PLUS / MINUS)? nl:NUMLIT {rule = nl}

strlit     <- STRLIT // todo: adapt for fstrings


/**************/
/* NODE KINDS */
/**************/

%node PROG
%node SHEBANG
%node NAMESPACE
%node GLOBALSCOPE

%node TEMPLATE
%node MEMBERLIST
%node STRUCT
%node TMPLSTRUCT
%node UNION
%node TMPLUNION
%node TRAIT
%node TMPLTRAIT
%node TYPE
%node TMPLTYPE
%node TRAITTYPE
%node STRUCTTYPE
%node VOIDPTR

%node QUEST
%node COLON

%node FOR
%node WHILE

%node CAST
%node REF
%node DEREF
%node BLK
%node ARGLIST
%node ARROW
%node LAMBDA

%node EQ
%node PLEQ
%node MINEQ
%node MULEQ
%node DIVEQ
%node MODEQ
%node ANDEQ
%node OREQ
%node XOREQ
%node BNEQ
%node BSREQ
%node BSLEQ

%node TERN
%node ELVIS
%node THEN
%node ALSO

%node LOGOR
%node LOGAND
%node OR
%node XOR
%node AND

%node DEQ
%node NEQ

%node LT
%node GT
%node LEQ
%node GEQ

%node BSL
%node BSR

%node STAR
%node MUL
%node DIV
%node MOD
%node POW

%node SUM
%node PLUS
%node MINUS

%node RET
%node GRAVE

%node CALL
%node TMPLCALL
%node FNARGLIST

%node SELF
%node SELFTYPE
%node VOIDTYPE
%node FNTYPE

%node OPEN
%node CLOSE
%node NUMLIT
%node STRLIT
%node TYPEMEMBER
%node STRUCTIDENT
%node DTRAITIDENT
%node VARIDENT
