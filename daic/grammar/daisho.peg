%include <assert.h>
%extra     void* extra;
%extra     void* symtab;
%extrainit node->extra = NULL;
%extrainit node->symtab = NULL;
%postinclude "../asthelpers.h"

file      <- sh:SHEBANG? topdecl (SEMI topdecl)*

/****************/
/* DECLARATIONS */
/****************/

topdecl    <- structdecl / uniondecl / traitdecl / fndecl / impldecl / nsdecl


structdecl <- STRUCT id:STRUCTIDENT tmpl:tmpldecl?
              impl:(IMPL type (COMMA type)*)?
              LCBRACK
              members:{ret=list(MEMBERLIST)}
             (m:typemember {add(members, m)})*
              RCBRACK
              n:{n = node(STRUCT, id, members);
              rule = (tmpl != SUCC) ? node(TMPLSTRUCT, tmpl, n) : n}

uniondecl  <- UNION id:STRUCTIDENT tmpl:tmplexpand?
              impl:(IMPL type (COMMA type)*)?
              LCBRACK
              members:{ret=list(MEMBERLIST)}
             (m:typemember {add(members, m)})*
              RCBRACK
              n:{n = node(UNION, id, members);
              rule = (tmpl != SUCC) ? node(TMPLUNION, tmpl, n) : n}

traitdecl  <- TRAIT id:STRUCTIDENT tmpl:tmplexpand?
              impl:(IMPL type (COMMA type)*)?
              LCBRACK
              members:{ret=list(MEMBERLIST)}
             (m:fnmember {add(members, m)})*
              RCBRACK
              n:{n = node(TRAIT, id, members);
              rule = (tmpl != SUCC) ? node(TMPLTRAIT, tmpl, n) : n}

fndecl     <- FN fnproto tmplexpand? expr

impldecl   <- IMPL tt:STRUCTIDENT tmplexpand?
              FOR ft:type
              LCBRACK
              members:{ret=list(MEMBERLIST)}
             (m:fnmember {add(members, m)})*
              RCBRACK

nsdecl     <- ns:NAMESPACE t:STRUCTIDENT {rule=node(NAMESPACE, t)}

typemember <- t:type v:VARIDENT SEMI {rule=node(TYPEMEMBER, t, v)}

fnmember   <- r:(fndecl / fnproto) SEMI {rule=r}


/*********/
/* TYPES */
/*********/

type<uint8_t depth = 0>
           <- v:VOIDTYPE     !STAR              {rule=node(TYPE, v)} // Void means nothing.
            / v:voidptr      (STAR {depth++})*  {rule=node(TYPE, v)} // Void* means anything.
            / s:SELFTYPE     (STAR {depth++})*  {rule=node(TYPE, s)} // Self refers to the type of the first arg, or the current type.
            / t:traittype    (STAR {depth++})*  {rule=node(TYPE, t)} // Trait object (+1 pointer, trait)
            / s:structtype   (STAR {depth++})*  {rule=node(TYPE, t)} // Type identifier
            / f:fntype       (STAR {depth++})*  {rule=node(TYPE, f)} // Fn<From -> To> is To(*fptr)(From).
            / c:ctypedecl                       {rule=node(TYPE, c)} // TODO figure out syntax

voidptr    <- v:VOIDPTR         {rule=v}
            / v:VOIDTYPE s:STAR {rule=leaf(VOIDPTR)}

traittype  <- i:STRUCTIDENT t:tmplexpand?
              {ret = t ? node(TMPLTYPE, t, i) : node(TYPE, i)}

structtype <- s:STRUCTIDENT t:tmplexpand?
              {ret = t ? node(TMPLTYPE, t, s) : node(TYPE, s)}

ctypedecl  <- CTYPE CIDENT

fntype     <- FNTYPE
              LT
              argtypes:{ret=list(ARGLIST)}
              (      t:type {add(argtypes, t)})?
              (COMMA t:type {add(argtypes, t)})*
              {if (!argtypes->num_children) add(argtypes, node(TYPE, leaf(VOIDTYPE)))}
              (arrow:ARROW rettype:type)?
              GT
              {rule=node(FNTYPE,
                         argtypes,
                         ((rettype==SUCC) | (rettype==NULL)) ? node(TYPE, leaf(VOIDTYPE)) : rettype)}
              /
              FNTYPE
              tmp:{rule=node(FNTYPE,
                            (tmp=list(ARGLIST), add(tmp, node(TYPE, leaf(VOIDTYPE))), tmp),
                             node(TYPE, leaf(VOIDTYPE)))}


/***********************/
/* FUNCTIONS AND TYPES */
/***********************/

tmpldecl   <- TEMPLATE

tmplspec   <-

tmplexpand <- LT tmplmember? (COMMA tmplmember)* GT

tmplmember <- type

fnproto    <- type OPEN fnarg? (COMMA fnarg)* CLOSE

fnarg      <- type (VARIDENT tmplexpand?)?

fnbody     <- expr


/****************************/
/* CONTROL FLOW EXPRESSIONS */
/****************************/

expr       <- next

forexpr    <- FOR OPEN? n:next ((COLON / IN) / (SEMI sn:next SEMI))  n:next CLOSE? n:next
            / n:next {rule=n}

whileexpr  <- WHILE o:OPEN? n:next c:CLOSE? {ret=o==c?SUCC:NULL} expr
            / n:next {rule=n}

ternexpr   <- n:next (q:QUEST qe:expr (c:COLON ce:expr)?)?
              {rule = !qe ? n
                    : !ce ? node(IF, q, n, qe)
                    :       node(TERN, q, c, n, qe, ce)}

thenexpr   <- n:next {rule=n} (o:THEN nn:next {rule=repr(node(THEN, rule, nn), o)})*

alsoexpr   <- n:next {rule=n} (o:ALSO nn:next {rule=repr(node(ALSO, rule, nn), o)})*


/********************/
/* BINARY OPERATORS */
/********************/

binop      <- next

eqexpr     <- n:next {rule=n}
             (op:(EQ / PLEQ / MINEQ / MULEQ / DIVEQ / MODEQ / ANDEQ / OREQ / XOREQ / BNEQ / BSREQ / BSLEQ)
              t:next
              // Requires information about what the next expression is
              {
                if      (op->kind == kind(EQ))    rule=node(EQ, rule,                       t );
                else if (op->kind == kind(PLEQ))  rule=node(EQ, rule, node(PLUS,  rule, t));
                else if (op->kind == kind(MINEQ)) rule=node(EQ, rule, node(MINUS, rule, t));
                else if (op->kind == kind(MULEQ)) rule=node(EQ, rule, node(MUL,   rule, t));
                else if (op->kind == kind(DIVEQ)) rule=node(EQ, rule, node(DIV,   rule, t));
                else if (op->kind == kind(MODEQ)) rule=node(EQ, rule, node(MOD,   rule, t));
                else if (op->kind == kind(ANDEQ)) rule=node(EQ, rule, node(AND,   rule, t));
                else if (op->kind == kind(OREQ))  rule=node(EQ, rule, node(OR,    rule, t));
                else if (op->kind == kind(XOREQ)) rule=node(EQ, rule, node(BNEQ,  rule, t));
                else if (op->kind == kind(BSREQ)) rule=node(EQ, rule, node(BSR,   rule, t));
                else if (op->kind == kind(BSLEQ)) rule=node(EQ, rule, node(BSL,   rule, t));
                else
                  #if defined(__DAI_UNREACHABLE)
                    __DAI_UNREACHABLE()
                  #else
                    assert(!"Unexpected node type.")
                  #endif
              })*

logorexpr  <- n:next {rule=n} (o:LOGOR  n:next {rule=repr(node(LOGOR,  rule, n), o)})*
logandexpr <- n:next {rule=n} (o:LOGAND n:next {rule=repr(node(LOGAND, rule, n), o)})*
binorexpr  <- n:next {rule=n} (o:OR     n:next {rule=repr(node(OR,     rule, n), o)})*
binxorexpr <- n:next {rule=n} (o:XOR    n:next {rule=repr(node(XOR,    rule, n), o)})*
binandexpr <- n:next {rule=n} (o:AND    n:next {rule=repr(node(AND,    rule, n), o)})*

deneqexpr  <- n:next {rule=n}
             (o:DEQ n:next {rule=repr(node(DEQ, rule, n), o)}
             /o:NEQ n:next {rule=repr(node(NEQ, rule, n), o)})*

cmpexpr    <- n:next {rule=n}
             (o:LT  n:next {rule=repr(node(LT,  rule, n), o)}
             /o:GT  n:next {rule=repr(node(GT,  rule, n), o)}
             /o:LEQ n:next {rule=repr(node(LEQ, rule, n), o)}
             /o:GEQ n:next {rule=repr(node(GEQ, rule, n), o)})*

shfexpr    <- n:next {rule=n} // Tokenized as << and >>.
             (l:LT lt:LT n:next {rule=node(BSL, l, lt, rule, n)}
             /g:GT gt:GT n:next {rule=node(BSR, g, gt, rule, n)})*

powexpr    <- n:next {rule=n} (p:POW {rule=repr(node(POW, rule, n), p)})*

multexpr   <- n:next {rule=n}
             (o:STAR n:next {rule=repr(node(STAR, rule, n), o)}
             /o:DIV  n:next {rule=repr(node(DIV,  rule, n), o)}
             /o:MOD  n:next {rule=repr(node(MOD,  rule, n), o)})*

sumexpr    <- n:next {rule=n}
             (o:PLUS  n:next {rule=repr(node(PLUS,  rule, n), o)}
             /o:MINUS n:next {rule=repr(node(MINUS, rule, n), o)} 
             
             {puts("");daisho_node_print_content(o, ctx->tokens)})*

callexpr   <- VARIDENT t:tmplexpand? OPEN CLOSE
            / n:next {rule=n}

castexpr   <- n:next (OPEN t:type CLOSE)? {rule = t ? node(CAST, t, n) : n}

refexpr    <- n:next r:REF?    {rule=(r != SUCC) ? node(REF, r, n) : n}

derefexpr  <- n:next d:DEREF?  {rule=(d != SUCC) ? node(REF, d, n) : n}

postretexpr <- n:next g:GRAVE? {rule=(g != SUCC) ? node(RET, g, n) : n}


/*********************************/
/* ATOMS AND SPECIAL EXPRESSIONS */
/*********************************/

atomexpr   <- blockexpr
            / lambdaexpr
            / listcomp
            / listlit
            / parenexpr
            / cfuncexpr
            / preretexpr
            / VARIDENT
            / numlit
            / strlit

blockexpr  <- LCBRACK
              {rule=list(BLK)}
              (e:expr {add(rule, e)})*
              RCBRACK

lambdaexpr <- (LSBRACK expr? (COMMA expr)* RSBRACK)? ((OPEN CLOSE) / (VARIDENT)) ARROW expr

listcomp   <- LSBRACK
              (cnt:VARIDENT COMMA)? expr   // Optional count
              FOR item:VARIDENT
              IN expr
              (WHERE expr)?
              RSBRACK

listlit    <- LSBRACK expr? (COMMA expr)* COMMA? RSBRACK

parenexpr  <- OPEN e:expr CLOSE {rule=e}

cfuncexpr  <- CFUNC CIDENT

preretexpr <- r:RET e:expr {rule=node(RET, r, e)}

numlit     <- pm:(PLUS / MINUS)? nl:NUMLIT {rule = nl}

strlit     <- STRLIT // todo: adapt for fstrings


/**************/
/* NODE KINDS */
/**************/

%node PROG
%node SHEBANG
%node NAMESPACE
%node GLOBALSCOPE

%node TEMPLATE
%node MEMBERLIST
%node STRUCT
%node TMPLSTRUCT
%node UNION
%node TMPLUNION
%node TRAIT
%node TMPLTRAIT
%node TYPE
%node TMPLTYPE
%node TRAITTYPE
%node STRUCTTYPE
%node VOIDPTR

%node QUEST
%node COLON

%node FOR
%node WHILE

%node CAST
%node REF
%node DEREF
%node BLK
%node ARGLIST
%node ARROW
%node LAMBDA

%node EQ
%node PLEQ
%node MINEQ
%node MULEQ
%node DIVEQ
%node MODEQ
%node ANDEQ
%node OREQ
%node XOREQ
%node BNEQ
%node BSREQ
%node BSLEQ

%node TERN
%node IF
%node THEN
%node ALSO

%node LOGOR
%node LOGAND
%node OR
%node XOR
%node AND

%node DEQ
%node NEQ

%node LT
%node GT
%node LEQ
%node GEQ

%node BSL
%node BSR

%node STAR
%node MUL
%node DIV
%node MOD
%node POW

%node SUM
%node PLUS
%node MINUS

%node RET
%node GRAVE

%node SELF
%node SELFTYPE
%node VOIDTYPE
%node FNTYPE

%node OPEN
%node CLOSE
%node NUMLIT
%node STRLIT
%node TYPEMEMBER
%node STRUCTIDENT
%node DTRAITIDENT
%node VARIDENT
